defmodule Craftplan.Catalog.Product do
  @moduledoc false
  use Ash.Resource,
    otp_app: :craftplan,
    domain: Craftplan.Catalog,
    data_layer: AshPostgres.DataLayer,
    authorizers: [Ash.Policy.Authorizer],
    extensions: [AshJsonApi.Resource, AshGraphql.Resource]

  alias Craftplan.Catalog.BOM

  json_api do
    type "product"

    routes do
      base("/products")
      get(:read)
      index :list
      post(:create)
      patch(:update)
      delete(:destroy)
    end
  end

  graphql do
    type :product

    queries do
      get(:get_product, :read)
      list(:list_products, :list)
    end

    mutations do
      create :create_product, :create
      update :update_product, :update
      destroy :destroy_product, :destroy
    end
  end

  postgres do
    table "catalog_products"
    repo Craftplan.Repo
  end

  actions do
    defaults [
      :read,
      :destroy,
      create: [
        :name,
        :status,
        :price,
        :sku,
        :photos,
        :featured_photo,
        :selling_availability,
        :max_daily_quantity,
        :nutrition_output_quantity,
        :nutrition_output_unit
      ],
      update: [
        :name,
        :status,
        :price,
        :sku,
        :photos,
        :featured_photo,
        :selling_availability,
        :max_daily_quantity,
        :nutrition_output_quantity,
        :nutrition_output_unit
      ]
    ]

    read :list do
      prepare build(sort: :name)

      argument :status, {:array, :atom} do
        allow_nil? true
        default nil
      end

      filter expr(is_nil(^arg(:status)) or status in ^arg(:status))

      pagination do
        required? false
        offset? true
        keyset? true
        countable true
      end
    end

    read :keyset do
      prepare build(sort: :name)
      pagination keyset?: true
    end
  end

  policies do
    # API key scope check
    policy always() do
      authorize_if {Craftplan.Accounts.Checks.ApiScopeCheck, []}
    end

    # Admin can do anything
    bypass expr(^actor(:role) == :admin) do
      authorize_if always()
    end

    # Public read for active/available products; staff/admin read everything
    policy action_type(:read) do
      authorize_if expr(status == :active or selling_availability != :off)
      authorize_if expr(^actor(:role) in [:staff, :admin])
    end

    # Writes restricted to staff/admin
    policy action_type([:create, :update, :destroy]) do
      authorize_if expr(^actor(:role) in [:staff, :admin])
    end
  end

  attributes do
    uuid_primary_key :id

    attribute :name, :string do
      allow_nil? false
      public? true

      constraints min_length: 2,
                  max_length: 100,
                  match: ~r/^[\p{L}\p{N}\w\s\-\.・（）「」()''&\x{2019}\x{2013}\x{2026}]+$/u
    end

    attribute :status, Craftplan.Catalog.Product.Types.Status do
      allow_nil? false
      public? true
      default :draft
    end

    attribute :price, :decimal do
      public? true
      allow_nil? false
    end

    attribute :sku, :string do
      allow_nil? false
      public? true
    end

    attribute :photos, {:array, :string} do
      public? true
      default []
      description "Array of photo URLs for the product"
    end

    attribute :featured_photo, :string do
      public? true
      allow_nil? true
      description "ID or reference to the featured photo from the photos array"
    end

    attribute :selling_availability, :atom do
      public? true
      allow_nil? false
      default :available
      constraints one_of: [:available, :preorder, :off]
      description "Customer-facing availability: available, preorder, or off"
    end

    attribute :max_daily_quantity, :integer do
      public? true
      allow_nil? false
      default 0
      constraints min: 0
      description "Optional per-product capacity per day (0 = unlimited)"
    end

    attribute :nutrition_output_quantity, :decimal do
      public? true
      constraints min: 0
      description "Finished product quantity used to express nutrition per 100g or 100ml."
    end

    attribute :nutrition_output_unit, :unit do
      public? true
      description "Finished product unit used to express nutrition per 100g or 100ml."
    end

    timestamps()
  end

  relationships do
    has_many :boms, BOM

    has_one :active_bom, BOM do
      filter expr(status == :active)
    end

    has_many :items, Craftplan.Orders.OrderItem
  end

  calculations do
    calculate :materials_cost, :decimal, Craftplan.Catalog.Product.Calculations.MaterialCost do
      description "Material cost per unit based on the active BOM."
    end

    calculate :bom_unit_cost, :decimal, Craftplan.Catalog.Product.Calculations.UnitCost do
      description "Total unit cost (materials + labor + overhead) derived from the active BOM."
    end

    calculate :markup_percentage,
              :decimal,
              Craftplan.Catalog.Product.Calculations.MarkupPercentage do
      description "The ratio of profit to cost, expressed as a decimal percentage"
    end

    calculate :gross_profit, :decimal, Craftplan.Catalog.Product.Calculations.GrossProfit do
      description "The profit amount calculated as selling price minus unit cost"
    end

    calculate :allergens, :vector, Craftplan.Catalog.Product.Calculations.Allergens

    calculate :nutritional_facts,
              :vector,
              Craftplan.Catalog.Product.Calculations.NutritionalFacts
  end

  identities do
    identity :sku, [:sku]
    identity :name, [:name]
  end
end
